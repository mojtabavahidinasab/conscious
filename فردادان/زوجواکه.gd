extends Control
## نام این بازی در Lumosity برابر Brain Shift است
##
## اگر نام شایسته‌تری دارید، خواهشمندم پیشنهاد دهید
## نشانی برای پیشنهاد نام شایسته TODO
##
## @tutorial:             https://github.com/mojtabavahidinasab/conscious/TODO

var امتیاز = 0

## برابر پارسی «audio» آویس می‌باشد. هنگام فشردن دکمه‌های برگشت و ایستادن، آویسی شانسی پخش خواهد شد.
var آویس‌های_دکمه = []


## همان پیشرفت. چه بازیکن درست باشد و چه نادرست پیشرفت برداشت می‌شود.
## بررسی درستی یا نادرستی بازیکن، دگرگونی متن برچسب‌ها و برگزیدن یکی از آنان کار ایشان می‌باشد.
func پیشروی(متغیر = null):
	
	## برابر پارسی «حروف صدادار» واکه می‌باشد.
	## به دانش من تنها واکه در زبان پارسی «الف» است و آن به مانند شماره یک می‌ماند.
	## بنابراین فقط در این بازی از زبان قاتل بهره می‌برم.
	var واکه‌ها = [
	"A", "E", "I", 
	"O", "U" 
	]
	
	## برابر پارسی «حروف بی‌صدا» هم‌خوان‌ها می‌باشد.
	## در ادامه مشکل واکه‌ها در پارسی، برای یکپارچگی از هم‌خوان‌های زبان قاتل بهره می‌برم.
	var همخوان‌ها = [
	"B", "C", "D",
	"F", "G", "H",
	"J", "K", "L",
	"M", "N", "P",
	"Q", "R", "S",
	"T", "V", "W",
	"X", "Y", "Z"
	]
	var زوج‌ها = [0, 2, 4, 6, 8]
	var فردها = [1, 3, 5, 7, 9]
	
	## هنگام آغاز بازی، این تابع فراخوانی می‌گردد.
	## تنها برای اینکه با هر بازآغاز این بازی، نخستین برچسب‌های نمایشی بازگو نشوند.
	## پس با هر بازآغاز، برچسب‌ها باید دگرگون گردند.
	## برای همین، این شرط از فرآیند بررسی درستی و نادرستی و امتیازدهی به هنگام آغاز جلوگیری می‌کند.
	if متغیر != null:
		
		## آیا زوج بودن شماره در برچسب بالایی پرسش بازی از بازیکن می‌باشد؟
		if $تن/زوج.get_theme_stylebox("normal").bg_color == Color.BLUE:
			
			## نخست اینکه این فرمان چه می‌کند؟
			## متنِ برچسب بالاتر یا زوج را به فهرستی تبدیل می‌کند و آن را اندوخته می‌سازد.
			## برای نمونه "1A" به ["1", "A"] دگرگون می‌گردد.
			## و چرا چنین کردم؟
			## نمی‌دانم چرایی این کارم را چگونه آشکار سازم. فردا می‌گویم. TODO
			var فهرست_واج‌های_برچسب_زوج = $"تن/زوج".text.split()
			for واج in فهرست_واج‌های_برچسب_زوج:
				if واج.is_valid_int():
					if متغیر and int(واج) in زوج‌ها:
						امتیاز += 1
						$"سر/امتیاز".text = str(امتیاز)
						آویس("درست")
					elif متغیر and int(واج) in فردها:
						آویس("نادرست")
					elif not متغیر and int(واج) in فردها:
						امتیاز += 1
						$"سر/امتیاز".text = str(امتیاز)
						آویس("درست")
					elif not متغیر and int(واج) in زوج‌ها:
						آویس("نادرست")
		else:
			
			## نخست اینکه این فرمان چه می‌کند؟
			## متنِ برچسب پایین‌تر یا واکه را به فهرستی تبدیل می‌کند و آن را اندوخته می‌سازد.
			## برای نمونه "1A" به ["1", "A"] دگرگون می‌گردد.
			## و چرا چنین کردم؟
			## نمی‌دانم چرایی این کارم را چگونه آشکار سازم. فردا می‌گویم. TODO
			var فهرست_واج‌های_برچسب_واکه = $"تن/زوج".text.split()
			for واج in فهرست_واج‌های_برچسب_واکه:
				if not واج.is_valid_int():
					if متغیر and واج in واکه‌ها:
						امتیاز += 1
						$"سر/امتیاز".text = str(امتیاز)
						آویس("درست")
					elif متغیر and واج in همخوان‌ها:
						آویس("نادرست")
					elif not متغیر and واج in همخوان‌ها:
						امتیاز += 1
						$"سر/امتیاز".text = str(امتیاز)
						آویس("درست")
					elif not متغیر and واج in واکه‌ها:
						آویس("نادرست")
	
	## پس از هر پیشروی/پیشرفت (یادآوری می‌کنم که بازیکن چه درست کرده باشد و چه نادرست، پیشرفت برداشت می‌گردد)
	## یکی از برچسب‌ها را برگزین تا بازیکن بررسی کند.
	## اگر برچسب بالاتر یا زوج برگزیده شود، کاربر باید پاسخ دهد که آیا شماره آشکار در آن زوج است؟
	## اگر برچسب پایین‌تر یا واکه برگزیده شود، کاربر باید پاسخ دهد که آیا واج آشکار در آن واکه است؟
	var متن‌ها = [$تن/زوج, $تن/واکه]
	if randi_range(0, 1):
		متن‌ها[0].get_theme_stylebox("normal").bg_color =  Color.WEB_GRAY
		متن‌ها[1].get_theme_stylebox("normal").bg_color =  Color.BLUE
		متن‌ها[0].get_theme_stylebox("normal").content_margin_bottom = 0
		متن‌ها[1].get_theme_stylebox("normal").content_margin_bottom = 77
	else:
		متن‌ها[1].get_theme_stylebox("normal").bg_color =  Color.WEB_GRAY
		متن‌ها[0].get_theme_stylebox("normal").bg_color =  Color.BLUE
		متن‌ها[0].get_theme_stylebox("normal").content_margin_bottom = 77
		متن‌ها[1].get_theme_stylebox("normal").content_margin_bottom = 0
	
	## پس از هر پیشروی/پیشرفت (یادآوری می‌کنم که بازیکن چه درست کرده باشد و چه نادرست، پیشرفت برداشت می‌گردد)
	## متن هر دو برچسب را دگرگون ساز.
	for متن in متن‌ها:
		if randi_range(0, 1):
			if randi_range(0, 1):
				متن.text = str(زوج‌ها.pick_random())
			else:
				متن.text = str(فردها.pick_random())
			if randi_range(0, 1):
				متن.text += همخوان‌ها.pick_random()
			else:
				متن.text += واکه‌ها.pick_random()
		else:
			if randi_range(0, 1):
				متن.text = همخوان‌ها.pick_random()
			else:
				متن.text = واکه‌ها.pick_random()
			if randi_range(0, 1):
				متن.text += str(زوج‌ها.pick_random())
			else:
				متن.text += str(فردها.pick_random())


## در آغازبازی یکبار متن‌های برچسب‌ها را دگرگون ساز.
## تا با هر بازآغاز همان متن‌های بازی پیش بازگو نگردند.
## نام این تابع یک واژه کلیدی می‌باشد برای همین نمی‌توان نامی فارسی جایگزین آن نمود.
func _ready():
	پیشروی()
	
	# اینها را نمی‌شد خارج از تابع نوشت. زیرا خطای زیر از این کار ایراد می‌گیرد:
	# Line X:Unexpected identifier "آویس‌های_دکمه" in class body.
	آویس‌های_دکمه.append(preload("res://جلوه آویسی/دکمه/بشکن۱.wav"))
	آویس‌های_دکمه.append(preload("res://جلوه آویسی/دکمه/بشکن۲.wav"))


## نام این تابع یک واژه کلیدی می‌باشد برای همین نمی‌توان نامی فارسی جایگزین آن نمود.
func _process(delta: float):
	if $شمارشگرآغاز.time_left:
		$شمارش.text = str(int($شمارشگرآغاز.time_left))
	$سر/زمان.text = str(int($شمارشگر.time_left))


func بایست():
	آویس("دکمه")
	if $شمارشگر.paused:
		$سر/ایستاننده.text = "  | |  "
	else:
		$سر/ایستاننده.text = "  <  "
	$شمارشگر.paused = not $شمارشگر.paused
	$پا.visible = not $پا.visible
	$ایستاده.visible = not $ایستاده.visible
	$ایستاده/امتیاز.text = str(امتیاز)
	$تن.visible = not $تن.visible


## هنگام پایان شمارشگر آغاز تابع فراخوانی می‌گردد
func آغازبازی():
	آویس("آغاز")
	$شمارش.hide()
	$پا/بله.disabled = false
	$پا/خیر.disabled = false
	$سر/ایستاننده.disabled = false
	$تن.show()
	$شمارشگر.start()


## هنگام لبریز شدن جام عمر بازی فراخوانی می‌شود
func پایان_بازی():
	$ایستاده.show()
	$سر.hide()
	$پا.hide()
	$تن.hide()
	$ایستاده/امتیاز.text = str(امتیاز)


func آویس(متغیر):
	if متغیر == "درست":
		$آویس.stream = preload("res://جلوه آویسی/درست.ogg")
		$آویس.play()
	elif متغیر == "نادرست":
		$آویس.stream = preload("res://جلوه آویسی/نادرست.wav")
		$آویس.play()
	elif متغیر == "پیروزی":
		$آویس.stream = preload("res://جلوه آویسی/پیروزی۱.wav")
		$آویس.play()
	elif متغیر == "باخت":
		$آویس.stream = preload("res://جلوه آویسی/باخت.wav")
		$آویس.play()
	elif متغیر == "آغاز":
		$آویس.stream = preload("res://جلوه آویسی/آغاز.mp3")
		$آویس.play()
	elif متغیر == "دکمه":
		$آویس.stream = آویس‌های_دکمه.pick_random()
		$آویس.play()


func برگرد():
	آویس("دکمه")
	get_tree().change_scene_to_file("res://صحنه‌ها/سرآغاز.tscn")
