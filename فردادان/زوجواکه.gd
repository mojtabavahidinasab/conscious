extends Control
## نام این بازی در Lumosity برابر Brain Shift است
##
## اگر نام شایسته‌تری دارید، خواهشمندم پیشنهاد دهید
## نشانی برای پیشنهاد نام شایسته TODO
##
## @tutorial:             https://github.com/mojtabavahidinasab/conscious/TODO

var امتیاز = 0


## همان پیشرفت. چه بازیکن درست باشد و چه نادرست پیشرفت برداشت می‌شود.
## بررسی درستی یا نادرستی بازیکن، دگرگونی متن برچسب‌ها و برگزیدن یکی از آنان کار ایشان می‌باشد.
func پیشروی(متغیر = null):
	
	## برابر پارسی «حروف صدادار» واکه می‌باشد.
	## به دانش من تنها واکه در زبان پارسی «الف» است و آن به مانند شماره یک می‌ماند.
	## بنابراین فقط در این بازی از زبان قاتل بهره می‌برم.
	var واکه‌ها = [
	"A", "E", "I", 
	"O", "U" 
	]
	
	## برابر پارسی «حروف بی‌صدا» هم‌خوان‌ها می‌باشد.
	## در ادامه مشکل واکه‌ها در پارسی، برای یکپارچگی از هم‌خوان‌های زبان قاتل بهره می‌برم.
	var همخوان‌ها = [
	"B", "C", "D",
	"F", "G", "H",
	"J", "K", "L",
	"M", "N", "P",
	"Q", "R", "S",
	"T", "V", "W",
	"X", "Y", "Z"
	]
	var زوج‌ها = [0, 2, 4, 6, 8]
	var فردها = [1, 3, 5, 7, 9]
	
	## هنگام آغاز بازی، این تابع فراخوانی می‌گردد.
	## تنها برای اینکه با هر بازآغاز این بازی، نخستین برچسب‌های نمایشی بازگو نشوند.
	## پس با هر بازآغاز، برچسب‌ها باید دگرگون گردند.
	## برای همین، این شرط از فرآیند بررسی درستی و نادرستی و امتیازدهی به هنگام آغاز جلوگیری می‌کند.
	if متغیر != null:
		var شماره‌زوج
		var واج‌زوج
		var شماره‌واکه
		var واج‌واکه
		for i in $"تن/زوج".text.split():
			if i.is_valid_int():
				شماره‌زوج = int(i)
			else:
				واج‌زوج = i
		for i in $"تن/واکه".text.split():
			if i.is_valid_int():
				شماره‌واکه = int(i)
			else:
				واج‌واکه = i
		## آیا زوج بودن شماره در برچسب بالایی پرسش بازی از بازیکن می‌باشد؟
		if $تن/زوج.get_theme_stylebox("normal").bg_color == Color.BLUE:
			if شماره‌زوج in زوج‌ها and متغیر or شماره‌زوج not in زوج‌ها and not متغیر:
				$آوا.سردادن("درست")
				امتیاز += 1
			else:
				$آوا.سردادن("نادرست")
		else:
			if واج‌واکه in واکه‌ها and متغیر or واج‌واکه not in واکه‌ها and not متغیر:
				$آوا.سردادن("درست")
				امتیاز += 1
			else:
				$آوا.سردادن("نادرست")
		$"ایستاده/امتیاز".text = پارسی‌سازی.شماره_پارسی(str(امتیاز))
		$"سر/امتیاز".text = "امتیاز: {0}".format([پارسی‌سازی.شماره_پارسی(str(امتیاز))])
	
	## پس از هر پیشروی/پیشرفت (یادآوری می‌کنم که بازیکن چه درست کرده باشد و چه نادرست، پیشرفت برداشت می‌گردد)
	## یکی از برچسب‌ها را برگزین تا بازیکن بررسی کند.
	## اگر برچسب بالاتر یا زوج برگزیده شود، کاربر باید پاسخ دهد که آیا شماره آشکار در آن زوج است؟
	## اگر برچسب پایین‌تر یا واکه برگزیده شود، کاربر باید پاسخ دهد که آیا واج آشکار در آن واکه است؟
	var متن‌ها = [$تن/زوج, $تن/واکه]
	if randi_range(0, 1):
		متن‌ها[0].get_theme_stylebox("normal").bg_color =  Color.WEB_GRAY
		متن‌ها[1].get_theme_stylebox("normal").bg_color =  Color.BLUE
		متن‌ها[0].get_theme_stylebox("normal").content_margin_bottom = 0
		متن‌ها[1].get_theme_stylebox("normal").content_margin_bottom = 77
	else:
		متن‌ها[1].get_theme_stylebox("normal").bg_color =  Color.WEB_GRAY
		متن‌ها[0].get_theme_stylebox("normal").bg_color =  Color.BLUE
		متن‌ها[0].get_theme_stylebox("normal").content_margin_bottom = 77
		متن‌ها[1].get_theme_stylebox("normal").content_margin_bottom = 0
	
	## پس از هر پیشروی/پیشرفت (یادآوری می‌کنم که بازیکن چه درست کرده باشد و چه نادرست، پیشرفت برداشت می‌گردد)
	## متن هر دو برچسب را دگرگون ساز.
	for متن in متن‌ها:
		if randi_range(0, 1):
			if randi_range(0, 1):
				متن.text = str(زوج‌ها.pick_random())
			else:
				متن.text = str(فردها.pick_random())
			if randi_range(0, 1):
				متن.text += همخوان‌ها.pick_random()
			else:
				متن.text += واکه‌ها.pick_random()
		else:
			if randi_range(0, 1):
				متن.text = همخوان‌ها.pick_random()
			else:
				متن.text = واکه‌ها.pick_random()
			if randi_range(0, 1):
				متن.text += str(زوج‌ها.pick_random())
			else:
				متن.text += str(فردها.pick_random())


## در آغازبازی یکبار متن‌های برچسب‌ها را دگرگون ساز.
## تا با هر بازآغاز همان متن‌های بازی پیش بازگو نگردند.
## نام این تابع یک واژه کلیدی می‌باشد برای همین نمی‌توان نامی فارسی جایگزین آن نمود.
func _ready():
	theme = بازی.سبک
	$"شمارشگر".start()
	$"سر/ایستاننده".pressed.connect($"سر".بایست.bind(get_children()))
	$"ایستاده/ادامه".pressed.connect($"ایستاده".ادامه_بازی.bind(get_children()))
	$"ایستاده/ازنو".pressed.connect($"ایستاده".بازی_ازنو)
	$"ایستاده/برگردان".pressed.connect($"ایستاده".بیرون_رفتن_ازبازی)
	پیشروی()


## نام این تابع یک واژه کلیدی می‌باشد برای همین نمی‌توان نامی فارسی جایگزین آن نمود.
func _process(delta: float):
	$سر/زمان.text = "زمان: {0}".format([پارسی‌سازی.شماره_پارسی(str(int($"شمارشگر".time_left)))])


## هنگام لبریز شدن جام عمر بازی فراخوانی می‌شود
func پایان_بازی():
	$"ایستاده/ادامه".hide()
	$ایستاده.show()
	$سر.hide()
	$پا.hide()
	$تن.hide()
	get_tree().paused = true
