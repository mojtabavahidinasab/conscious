extends Control
## نام این بازی در Lumosity برابر Color Match است
##
## اگر نام شایسته‌تری دارید، خواهشمندم پیشنهاد دهید
## نشانی برای پیشنهاد نام شایسته TODO
##
## @tutorial:             https://github.com/mojtabavahidinasab/conscious/TODO

var امتیاز = 0


## همان پیشرفت. چه بازیکن درست باشد و چه نادرست پیشرفت برداشت می‌شود.
## بررسی درستی یا نادرستی بازیکن و دگرگونی متن برچسب‌ها کار ایشان می‌باشد.
func پیشروی(متغیر = null):
	var رنگ‌ها = {
		"آبی": Color.BLUE,
		"مشکی": Color.BLACK,
		"سفید": Color.WHITE,
		"زرد": Color.YELLOW,
		"سبز": Color.GREEN,
		"قرمز": Color.RED
		}
	if متغیر != null:
		if رنگ‌ها[$تن/متن.text] == $تن/رنگ.get("theme_override_colors/font_color"):
			if متغیر:
				$آوا.سردادن("درست")
				امتیاز += 1
				$سر/امتیاز.text = "امتیاز: {0}".format([پارسی‌سازی.شماره_پارسی(امتیاز)])
				$"ایستاده/امتیاز".text = پارسی‌سازی.شماره_پارسی(امتیاز)
			else:
				$آوا.سردادن("نادرست")
		else:
			if not متغیر:
				$آوا.سردادن("درست")
				امتیاز += 1
				$سر/امتیاز.text = "امتیاز: {0}".format([پارسی‌سازی.شماره_پارسی(امتیاز)])
				$"ایستاده/امتیاز".text = پارسی‌سازی.شماره_پارسی(امتیاز)
			else:
				$آوا.سردادن("نادرست")
	$تن/متن.set("theme_override_colors/font_color", رنگ‌ها.values().pick_random())
	$تن/رنگ.set("theme_override_colors/font_color", رنگ‌ها.values().pick_random())
	$تن/متن.text = رنگ‌ها.keys().pick_random()
	$تن/رنگ.text = رنگ‌ها.keys().pick_random()


## در آغازبازی یکبار متن‌های برچسب‌ها را دگرگون ساز.
## تا با هر بازآغاز همان متن‌های بازی پیش بازگو نگردند.
## نام این تابع یک واژه کلیدی می‌باشد برای همین نمی‌توان نامی فارسی جایگزین آن نمود.
func _ready() -> void:
	$"شمارشگر".start()
	$"سر/ایستاننده".pressed.connect($"سر".بایست.bind(get_children()))
	$"ایستاده/ادامه".pressed.connect($"ایستاده".ادامه_بازی.bind(get_children()))
	$"ایستاده/ازنو".pressed.connect($"ایستاده".بازی_ازنو)
	$"ایستاده/برگردان".pressed.connect($"ایستاده".بیرون_رفتن_ازبازی)
	پیشروی()


## نام این تابع یک واژه کلیدی می‌باشد برای همین نمی‌توان نامی فارسی جایگزین آن نمود.
func _process(delta: float):
	$سر/زمان.text = "زمان: {0}".format([پارسی‌سازی.شماره_پارسی(int($"شمارشگر".time_left))])


## هنگام لبریز شدن جام عمر بازی فراخوانی می‌شود
func پایان_بازی():
	$"ایستاده/ادامه".hide()
	$ایستاده.show()
	$سر.hide()
	$پا.hide()
	$تن.hide()
	$ایستاده/امتیاز.text = پارسی‌سازی.شماره_پارسی(امتیاز)
	get_tree().paused = true
