extends Control
## نام این بازی در Lumosity برابر Raindrops است
##
## اگر نام شایسته‌تری دارید، خواهشمندم پیشنهاد دهید
## نشانی برای پیشنهاد نام شایسته TODO
##
## @tutorial:             https://github.com/mojtabavahidinasab/conscious/TODO

var امتیاز = 0
var امتیاز_ضرب = 0
var امتیاز_تقسیم = 0
var کران_پایین_تندی_بارش = 4
var کران_بالای_تندی_بارش = 7
var جان = 3
var قطرات = []
@export var قطره: PackedScene
var کارها = {
	"جمع": "➕",  #  / ➕
	"تفریق": "",  # ➖ / 
	"ضرب": "✖️",
	"تقسیم": "➗"  # ➗
} 
var کران_پایین_جمع = 0
var کران_بالای_جمع = 10
var کران_پایین_ضرب = 0
var کران_بالای_ضرب = 5
var کران_پایین_تقسیم = 0
var کران_بالای_تقسیم = 2
var ضریب_امتیازتفریق = 2
var ضریب_امتیازعلامت = 2
var ضریب_امتیازضرب = 5
var ضریب_امتیازتقسیم = 10
var شمارگان = {
	".": "٫",  # 1.2 = ۱٫۲ 
	0: "۰",
	1: "۱",
	2: "۲",
	3: "۳",
	4: "۴",
	5: "۵",
	6: "۶",
	7: "۷",
	8: "۸",
	9: "۹"
}
var نوشتار = ""


## برابر پارسی «audio» آویس می‌باشد. هنگام فشردن دکمه‌های برگشت و ایستادن، آویسی شانسی پخش خواهد شد.
var آویس‌های_دکمه = []


## نام این تابع یک واژه کلیدی می‌باشد برای همین نمی‌توان نامی فارسی جایگزین آن نمود.
func _ready():
	$"آب".hide()
	$"ایستاده".hide()
	$"تخته‌کلید".hide()
	# اینها را نمی‌شد خارج از تابع نوشت. زیرا خطای زیر از این کار ایراد می‌گیرد:
	# Line X:Unexpected identifier "آویس‌های_دکمه" in class body.
	آویس‌های_دکمه.append(preload("res://جلوه آویسی/دکمه/بشکن۱.wav"))
	آویس‌های_دکمه.append(preload("res://جلوه آویسی/دکمه/بشکن۲.wav"))


func بارش():
	var نمونه = قطره.instantiate()
	var کار = کارها.values().pick_random()
	var علامت۱ = [-1, 1].pick_random()
	var علامت۲ = [-1, 1].pick_random()
	var ضریب = 1
	if علامت۱ == -1:
		ضریب *= ضریب_امتیازعلامت
	if علامت۲ == -1:
		ضریب *= ضریب_امتیازعلامت
	if کار == کارها["جمع"]:
		var شماره۱ = randi_range(کران_پایین_جمع, کران_بالای_جمع) * علامت۱
		var شماره۲ = randi_range(کران_پایین_جمع, کران_بالای_جمع) * علامت۲
		var پاسخ = شماره۱ + شماره۲
		نمونه.بروزرسانی(کار, شماره۱, شماره۲, پاسخ, ضریب)
	elif کار == کارها["تفریق"]:
		ضریب *= ضریب_امتیازتفریق
		var شماره۱ = randi_range(کران_پایین_جمع, کران_بالای_جمع) * علامت۱
		var شماره۲ = randi_range(کران_پایین_جمع, کران_بالای_جمع) * علامت۲
		var پاسخ = شماره۱ - شماره۲
		نمونه.بروزرسانی(کار, شماره۱, شماره۲, پاسخ, ضریب)
	elif کار == کارها["ضرب"]:
		ضریب *= ضریب_امتیازضرب
		var شماره۱ = randi_range(کران_پایین_ضرب, کران_بالای_ضرب) * علامت۱
		var شماره۲ = randi_range(کران_پایین_ضرب, کران_بالای_ضرب) * علامت۲
		var پاسخ = شماره۱ * شماره۲
		نمونه.بروزرسانی(کار, شماره۱, شماره۲, پاسخ, ضریب)
	elif کار == کارها["تقسیم"]:
		ضریب *= ضریب_امتیازتقسیم
		var شماره۱ = randi_range(کران_پایین_تقسیم, کران_بالای_تقسیم) * علامت۱
		var شماره۲ = randi_range(کران_پایین_تقسیم, کران_بالای_تقسیم) * علامت۲
		if شماره۲ == 0:
			شماره۲ += 1
		var پاسخ = snappedf(شماره۱ / شماره۲, .01)
		نمونه.بروزرسانی(کار, شماره۱, شماره۲, پاسخ, ضریب)
	قطرات.append(نمونه)
	نمونه.z_index = -1
	var مکان = $"راه/راه قطره"
	مکان.progress_ratio = randf()
	نمونه.position = مکان.position
	add_child(نمونه)
	$"شمارشگر".wait_time = randf_range(کران_پایین_تندی_بارش, کران_بالای_تندی_بارش)
	$"شمارشگر".start()


## نام این تابع یک واژه کلیدی می‌باشد برای همین نمی‌توان نامی فارسی جایگزین آن نمود.
func _process(delta):
	if $شمارشگرآغاز.time_left:
		$شمارش.text = str(int($شمارشگرآغاز.time_left))


## هنگام لبریز شدن جام عمر بازی فراخوانی می‌شود
func پایان_بازی():
	$ایستاده.show()
	$سر.hide()
	$ایستاده/امتیاز.text = str(امتیاز)
	$"شمارشگر".paused = true
	$"تخته‌کلید".hide()
	for هرقطره in قطرات:
		هرقطره.free()
	قطرات.clear()
	$"آب".hide()


## هنگام پایان شمارشگر آغاز تابع فراخوانی می‌گردد
func آغازبازی():
	آویس("آغاز")
	$"تخته‌کلید".show()
	$"آب".show()
	$"آب/پویانمایی".play()
	$شمارش.hide()
	$سر/ایستاننده.disabled = false
	$"شمارشگر".start()


func بایست():
	آویس("دکمه")
	$ایستاده.visible = not $ایستاده.visible
	$"تخته‌کلید".visible = not $"تخته‌کلید".visible
	$"آب".visible = not $"آب".visible
	$"شمارشگر".paused = not $"شمارشگر".paused
	for هرقطره in قطرات:
		هرقطره.freeze = not هرقطره.freeze
		هرقطره.visible = not هرقطره.visible
	if $ایستاده.visible:
		$سر/ایستاننده.text = "  <  "
	else:
		$سر/ایستاننده.text = "  | |  "
	$ایستاده/امتیاز.text = str(امتیاز)


func برگرد():
	آویس("دکمه")
	get_tree().change_scene_to_file("res://صحنه‌ها/سرآغاز.tscn")


func آویس(متغیر):
	if متغیر == "درست":
		$آویس.stream = preload("res://جلوه آویسی/درست.ogg")
		$آویس.play()
	elif متغیر == "نادرست":
		$آویس.stream = preload("res://جلوه آویسی/نادرست.wav")
		$آویس.play()
	elif متغیر == "پیروزی":
		$آویس.stream = preload("res://جلوه آویسی/پیروزی۱.wav")
		$آویس.play()
	elif متغیر == "باخت":
		$آویس.stream = preload("res://جلوه آویسی/باخت.wav")
		$آویس.play()
	elif متغیر == "آغاز":
		$آویس.stream = preload("res://جلوه آویسی/آغاز.mp3")
		$آویس.play()
	elif متغیر == "دکمه":
		$آویس.stream = آویس‌های_دکمه.pick_random()
		$آویس.play()


func برخورد(پیکر: Node2D):
	قطرات.erase(پیکر)
	پیکر.queue_free()
	$"آب".scale *= 2
	جان -= 1
	if not جان:
		پایان_بازی()


func بنویس(نویسه):
	if نویسه is int:
		نوشتار += str(نویسه)
		$"تخته‌کلید/شماره".text += شمارگان[نویسه]
		return
	elif نویسه is String:
		if نویسه == ".":
			نوشتار += نویسه
			$"تخته‌کلید/شماره".text += شمارگان[نویسه]
		elif نویسه == "-" and نوشتار.begins_with("-"):
			نوشتار = نوشتار.replace("-", "")
			$"تخته‌کلید/شماره".text = $"تخته‌کلید/شماره".text.replace("-", "")
		elif نویسه == "-" and not نوشتار.begins_with("-"):
			نوشتار = "-" + نوشتار
			$"تخته‌کلید/شماره".text = "-" + $"تخته‌کلید/شماره".text
		return
	if نویسه:
		for هرقطره in قطرات:
			if snapped(float(نوشتار), .01) == هرقطره.پاسخ:
				آویس("درست")
				امتیاز += هرقطره.ضریب_امتیاز
				if هرقطره.کار == کارها["ضرب"]:
					امتیاز_ضرب += 1
				if هرقطره.کار == کارها["تقسیم"]:
					امتیاز_تقسیم += 1
				هرقطره.free()
				قطرات.erase(هرقطره)
				$"سر/امتیاز".text = str(امتیاز)
	نوشتار = ""
	$"تخته‌کلید/شماره".text = ""
	if امتیاز_تقسیم == کران_بالای_تقسیم:
		کران_بالای_تقسیم *= 2
	if امتیاز_ضرب == کران_بالای_ضرب - int(کران_بالای_ضرب / 10):
		کران_بالای_ضرب *= 2
	if امتیاز == int(کران_بالای_جمع / 2):
		کران_بالای_جمع *= 3
	if not امتیاز % 5 and کران_پایین_تندی_بارش >= 1:
		کران_بالای_تندی_بارش -= .2
	if کران_پایین_تندی_بارش - کران_بالای_تندی_بارش < 1:
		کران_پایین_تندی_بارش -= .2
	
